module OperationMuv exposing (Operation, Msg, Model, Notification(..), update, initModel, operationEncoder, operationDecoder, viewOperations)

import Html exposing (..)
import Html.Attributes exposing (..)
import Html.Events exposing (onClick, onInput, onBlur)
import Json.Decode exposing (Decoder)
import Json.Decode.Extra
import Json.Encode


{--
This file contains an operation related MUV structure (Model Update View)
The view handles the display of operation list in a table
and of single operation in a modal

Module exposes:
* Operation type and decoder for use in Budget type
* Operation subModel, subMsg, update and view
--}



{-------------------------
        MODEL
--------------------------}
type alias Model =
    { content: OperationStatus 
    , modal: Modal }

initModel =
    Model NoOperation NoModal



{-------------------------
        TYPES
--------------------------}
type Notification 
    = NoNotification
    | SendPutRequest Operation

type alias Operation =
    { id: Int
    , name: String
    , operationType: OperationType
    , store: String
    , comment: Maybe String
    , quotation: Quotation
    , invoice: Invoice
    }

{-- private types --}
type OperationStatus
    = NoOperation
    | IdOnly Int
    | Validated Operation

type OperationType 
    = Credit
    | Debit

type alias Quotation =
    { quotationReference: Maybe String
    , quotationDate: Maybe String
    , quotationAmount: AmountField
    }

-- allows to use number in input field
type alias AmountField = 
    { value: Maybe Float
    , stringValue: String
    }

type alias Invoice =
    { invoiceReference: Maybe String
    , invoiceDate: Maybe String
    , invoiceAmount: AmountField
    }

type Modal 
    = NoModal
    | DisplayOperationModal
    | ModifyOperationModal
    | CreateOperationModal




{-------------------------
        UPDATE
--------------------------}

type Msg
    = SelectOperationClicked Int
    | CloseOperationModalClicked
    | ModifyOperationClicked Operation
    | SaveModifiedOperationClicked
    | SetName String
    | SetQuotationReference String
    | SetQuotationDate String
    | SetQuotationAmount String
    | SetInvoiceReference String
    | SetInvoiceDate String
    | SetInvoiceAmount String
    | SetStore String
    | SetComment String

update : Msg -> Model -> ( Model, Notification, Cmd Msg )
update msg model =
    case msg of
        SelectOperationClicked operationId ->
            ( { model | modal = DisplayOperationModal, content = IdOnly operationId }
            , NoNotification
            , Cmd.none )
        
        CloseOperationModalClicked ->
            ( { model | modal = NoModal, content = NoOperation }
            , NoNotification
            , Cmd.none)
        
        ModifyOperationClicked operation ->
            ( { model | modal = ModifyOperationModal, content = Validated operation }
            , NoNotification
            , Cmd.none )
        
        SaveModifiedOperationClicked ->
            case model.content of
                Validated operation -> 
                    ( { model | modal = NoModal, content = Validated operation }
                    , SendPutRequest operation
                    , Cmd.none )

                _ -> ( { model | modal = NoModal, content = NoOperation }
                    , NoNotification
                    , Cmd.none )

        SetName value ->
            case model.content of
                Validated operation -> let 
                                            newContent = { operation | name = value} 
                                        in
                                            ( { model | content = Validated newContent }
                                            , NoNotification
                                            , Cmd.none)
                _ -> (model, NoNotification, Cmd.none)

        SetQuotationReference value ->
            case model.content of
                Validated operation -> let 
                                            oldQuotation = operation.quotation
                                            newQuotation = { oldQuotation | quotationReference = convertStringToMaybeString value }
                                            newContent = { operation | quotation = newQuotation } 
                                        in
                                            ( { model | content = Validated newContent }
                                            , NoNotification
                                            , Cmd.none)
                _ -> (model, NoNotification, Cmd.none)

        SetQuotationDate value ->
            case model.content of
                Validated operation -> let 
                                            oldQuotation = operation.quotation
                                            newQuotation = { oldQuotation | quotationDate = convertStringToMaybeString value }
                                            newContent = { operation | quotation = newQuotation } 
                                        in
                                            ( { model | content = Validated newContent }
                                            , NoNotification
                                            , Cmd.none)
                _ -> (model, NoNotification, Cmd.none)

        SetQuotationAmount value ->
            case model.content of
                Validated operation -> 
                    case (String.toFloat value) of
                        Just amount -> let 
                                            oldQuotation = operation.quotation
                                            newQuotation = { oldQuotation | quotationAmount = AmountField (Just amount) value }
                                            newContent = { operation | quotation = newQuotation } 
                                        in
                                            ( { model | content = Validated newContent }
                                            , NoNotification
                                            , Cmd.none)
                        Nothing -> let 
                                        oldQuotation = operation.quotation
                                        newQuotation = { oldQuotation | quotationAmount = AmountField Nothing value }
                                        newContent = { operation | quotation = newQuotation } 
                                    in
                                        ( { model | content = Validated newContent }
                                        , NoNotification
                                        , Cmd.none)
                _ -> (model, NoNotification, Cmd.none)

        SetInvoiceReference value ->
            case model.content of
                Validated operation -> let 
                                            oldInvoice = operation.invoice
                                            newInvoice = { oldInvoice | invoiceReference = convertStringToMaybeString value }
                                            newContent = { operation | invoice = newInvoice } 
                                        in
                                            ( { model | content = Validated newContent }
                                            , NoNotification
                                            , Cmd.none)
                _ -> (model, NoNotification, Cmd.none)

        SetInvoiceDate value ->
            case model.content of
                Validated operation -> let 
                                            oldInvoice = operation.invoice
                                            newInvoice = { oldInvoice | invoiceDate = convertStringToMaybeString value }
                                            newContent = { operation | invoice = newInvoice } 
                                        in
                                            ( { model | content = Validated newContent }
                                            , NoNotification
                                            , Cmd.none)
                _ -> (model, NoNotification, Cmd.none)

        SetInvoiceAmount value ->
            case model.content of
                Validated operation -> 
                    case (String.toFloat value) of
                        Just amount -> let 
                                            oldInvoice = operation.invoice
                                            newInvoice = { oldInvoice | invoiceAmount = AmountField (Just amount) value }
                                            newContent = { operation | invoice = newInvoice } 
                                        in
                                            ( { model | content = Validated newContent }
                                            , NoNotification
                                            , Cmd.none)
                        Nothing -> let 
                                        oldInvoice = operation.invoice
                                        newInvoice = { oldInvoice | invoiceAmount = AmountField Nothing value }
                                        newContent = { operation | invoice = newInvoice } 
                                    in
                                        ( { model | content = Validated newContent }
                                        , NoNotification
                                        , Cmd.none)
                _ -> (model, NoNotification, Cmd.none)

        SetStore value ->
            case model.content of
                Validated operation -> let 
                                            newContent = { operation | store = value} 
                                        in
                                            ( { model | content = Validated newContent }
                                            , NoNotification
                                            , Cmd.none)
                _ -> (model, NoNotification, Cmd.none)

        SetComment value ->
            case model.content of
                Validated operation -> let 
                                            newContent = { operation | comment = convertStringToMaybeString value } 
                                        in
                                            ( { model | content = Validated newContent }
                                            , NoNotification
                                            , Cmd.none)
                _ -> (model, NoNotification, Cmd.none)

convertStringToMaybeString: String -> Maybe String
convertStringToMaybeString stringToConvert =
    case stringToConvert of
        "" -> Nothing
        _ -> Just stringToConvert


{-------------------------
        DECODER
--------------------------}
operationDecoder: Decoder Operation
operationDecoder =
    Json.Decode.succeed Operation
        |> Json.Decode.Extra.andMap (Json.Decode.field "id" Json.Decode.int)
        |> Json.Decode.Extra.andMap (Json.Decode.field "name" Json.Decode.string)
        |> Json.Decode.Extra.andMap (Json.Decode.field "type" operationTypeDecoder)
        |> Json.Decode.Extra.andMap (Json.Decode.field "store" Json.Decode.string)
        |> Json.Decode.Extra.andMap (Json.Decode.field "comment" (Json.Decode.nullable Json.Decode.string))
        |> Json.Decode.Extra.andMap (Json.Decode.map3 Quotation 
                (Json.Decode.field "quotation" (Json.Decode.nullable Json.Decode.string)) 
                (Json.Decode.field "quotationDate" (Json.Decode.nullable dateDecoder)) 
                (Json.Decode.field "quotationAmount" amountDecoder))
        |> Json.Decode.Extra.andMap (Json.Decode.map3 Invoice 
                (Json.Decode.field "invoice" (Json.Decode.nullable Json.Decode.string)) 
                (Json.Decode.field "invoiceDate" (Json.Decode.nullable dateDecoder))
                (Json.Decode.field "invoiceAmount" amountDecoder))

operationTypeDecoder: Decoder OperationType
operationTypeDecoder =
    Json.Decode.string
        |> Json.Decode.andThen operationTypeStringDecoder

operationTypeStringDecoder: String -> Decoder OperationType
operationTypeStringDecoder typeString =
    case String.toLower(typeString) of
        "credit" -> Json.Decode.succeed Credit
        "debit" -> Json.Decode.succeed Debit
        _ -> Json.Decode.fail ("Error while decoding operationType: " ++ typeString)

dateDecoder: Decoder String
dateDecoder =
    Json.Decode.succeed toDateString
        |> Json.Decode.Extra.andMap (Json.Decode.field "dayOfMonth" Json.Decode.int)
        |> Json.Decode.Extra.andMap (Json.Decode.field "monthOfYear" Json.Decode.int)
        |> Json.Decode.Extra.andMap (Json.Decode.field "yearOfEra" Json.Decode.int)

toDateString : Int -> Int -> Int -> String
toDateString day month year =
    String.join "/" [ String.fromInt(day)
                    , String.fromInt(month)
                    , String.fromInt(year)]

amountDecoder : Decoder AmountField
amountDecoder =
    Json.Decode.nullable Json.Decode.int
        |> Json.Decode.andThen amountFieldDecoder

amountFieldDecoder : Maybe Int -> Decoder AmountField
amountFieldDecoder maybeAmount =
    case (centsToEuros maybeAmount) of
        Just amount -> Json.Decode.succeed <| AmountField (Just amount) (String.fromFloat amount)
        Nothing -> Json.Decode.succeed <| AmountField Nothing ""

centsToEuros: Maybe Int -> Maybe Float
centsToEuros maybeAmount =
    case maybeAmount of
        Just amount -> Just (toFloat amount / 100)
        Nothing -> Nothing


{-------------------------
        ENCODER
--------------------------}
operationEncoder: Operation -> Json.Encode.Value
operationEncoder operation =
  Json.Encode.object 
    [ ("id", Json.Encode.int operation.id)
    , ("name", Json.Encode.string operation.name)
    , ("type", encodeOperationType operation.operationType)
    , ("store", Json.Encode.string operation.store)
    , ("comment", encodeMaybeString operation.comment)
    , ("quotation", encodeMaybeString operation.quotation.quotationReference)
    , ("quotationDate", encodeMaybeString operation.quotation.quotationDate)
    , ("quotationAmount", encodeAmount operation.quotation.quotationAmount)
    , ("invoice", encodeMaybeString operation.invoice.invoiceReference)
    , ("invoiceDate", encodeMaybeString operation.invoice.invoiceDate)
    , ("invoiceAmount", encodeAmount operation.invoice.invoiceAmount)
    ]

encodeOperationType: OperationType -> Json.Encode.Value
encodeOperationType type_ =
    case type_ of
        Credit -> Json.Encode.string "credit"
        Debit -> Json.Encode.string "debit"

encodeMaybeString: Maybe String -> Json.Encode.Value
encodeMaybeString maybeString =
    case maybeString of
        Just value -> Json.Encode.string value
        Nothing -> Json.Encode.null

encodeAmount: AmountField -> Json.Encode.Value
encodeAmount amountField =
    encodeMaybeFloat amountField.value

encodeMaybeFloat: Maybe Float -> Json.Encode.Value
encodeMaybeFloat maybeFloat =
    case maybeFloat of
        Just value -> Json.Encode.int <| euroToCents value
        Nothing -> Json.Encode.null

euroToCents: Float -> Int
euroToCents floatAmount =
    round <| floatAmount * 100


{-------------------------
        VIEW
--------------------------}

-- VIEW ALL OPERATIONS IN A TABLE
viewOperations: List Operation -> Model -> Html Msg
viewOperations operations operationModel =
    div [] [table [ class "table is-budget-tab-content is-striped is-hoverable is-fullwidth"]
                  [ viewOperationsHeaderRow 
                  , viewOperationsRows operations ]
            , viewOperationModal operations operationModel ]

viewOperationsHeaderRow: Html Msg
viewOperationsHeaderRow =
    let
        columnNames = [ "nom"
                      , "n째 devis"
                      , "date devis"
                      , "montant devis"
                      , "n째 facture"
                      , "date facture"
                      , "montant facture"
                      , "fournisseur"
                      , "commentaire"
                      ]
    in
        thead [] [ tr [] (List.map viewOperationsHeaderCell columnNames)]

viewOperationsHeaderCell: String -> Html Msg
viewOperationsHeaderCell cellContent =
    th [] [text cellContent]

viewOperationsRows: List Operation -> Html Msg
viewOperationsRows operations =
    tbody [] (List.map viewOperationsRow operations)

viewOperationsRow: Operation -> Html Msg
viewOperationsRow operation =
        tr [ onClick <| SelectOperationClicked operation.id ] [ th [] [text operation.name]
                , td [] [text <| Maybe.withDefault "" operation.quotation.quotationReference ]
                , td [] [text <| Maybe.withDefault "" operation.quotation.quotationDate ]
                , td [] [text <| operation.quotation.quotationAmount.stringValue ]
                , td [] [text <| Maybe.withDefault "" operation.invoice.invoiceReference ]
                , td [] [text <| Maybe.withDefault "" operation.invoice.invoiceDate ]
                , td [] [text operation.invoice.invoiceAmount.stringValue ]
                , td [] [text operation.store ]
                , td [] [text <| Maybe.withDefault "" operation.comment ]
            ]


maybeFloatToMaybeString: Maybe Float -> Maybe String
maybeFloatToMaybeString maybeFloat =
    case maybeFloat of
        Just float -> Just(String.fromFloat float)
        Nothing -> Nothing



-- SELECT OPERATION TO DISPLAY IN MODAL
viewOperationModal : List Operation -> Model -> Html Msg
viewOperationModal operations operationModel =
    case (operationModel.modal, operationModel.content) of
        (NoModal, _) -> emptyDiv
        (_ , IdOnly operationId) -> 
            let
                operationToDisplay = getOperationById operationId operations
            in 
                case operationToDisplay of
                    Just operation -> displayOperationModal operation DisplayOperationModal
                    Nothing -> emptyDiv
        
        (_, Validated operation) -> 
            displayOperationModal operation ModifyOperationModal
        
        (_, _) -> emptyDiv

emptyDiv : Html Msg
emptyDiv = div [] []

getOperationById: Int -> List Operation -> Maybe Operation
getOperationById operationId operations =
    List.filter (\ op -> (op.id == operationId)) operations
        |> getSingleOperation

getSingleOperation: List Operation -> Maybe Operation
getSingleOperation operations =
    if (List.length operations) == 1 then List.head operations else Nothing


-- VIEW OPERATION IN A EDITABLE OR READ-ONLY MODAL
displayOperationModal : Operation -> Modal -> Html Msg
displayOperationModal operation modal =
    div [class "modal is-operation-modal"]
        [div [class "modal-background"][]
        ,div [class "modal-card"]
            [header [class "modal-card-head"]
                    (viewOperationHeader operation modal)
            ,section [class "modal-card-body"]
                     [table [class "table is-budget-tab-content is-striped is-hoverable is-fullwidth"]
                            [ viewOperationBody operation modal]
                     ]
            ,footer [class "modal-card-foot"]
                    (viewOperationFooter modal)
            ]
        ]

viewOperationHeader: Operation -> Modal -> List (Html Msg)
viewOperationHeader operation modal =
    case modal of
        DisplayOperationModal -> [p [class "modal-card-title"] [ text operation.name ]
                                    ,button [class "button is-rounded is-success", onClick <| ModifyOperationClicked operation] 
                                            [span [class "icon is-small"] 
                                                  [i [class "fas fa-pencil-alt"] []]
                                            ]
                                    ,button [class "button is-rounded", onClick CloseOperationModalClicked]
                                            [span [class "icon is-small"] 
                                                  [i [class "fas fa-times"] []]
                                            ]
                                    ]
        _ -> [p [class "modal-card-title"] [ text operation.name ]] 

viewOperationBody: Operation -> Modal -> Html Msg
viewOperationBody operation modal =
    case modal of
        DisplayOperationModal -> viewOperationFields operation viewOperationReadOnly
        ModifyOperationModal -> viewOperationFields operation viewOperationInput
        _ -> emptyDiv 


-- according to the type of the modal use readOnly or Input fields to view operation details
viewOperationFields: Operation -> (String -> (String -> Msg) -> String -> Html Msg) -> Html Msg
viewOperationFields operation callback =
        tbody [] [callback "nom" SetName operation.name
                , callback "n째 devis" SetQuotationReference <| Maybe.withDefault "" operation.quotation.quotationReference
                , callback "date du devis" SetQuotationDate <| Maybe.withDefault "" operation.quotation.quotationDate
                , callback "montant du devis" SetQuotationAmount operation.quotation.quotationAmount.stringValue
                , callback "n째 facture" SetInvoiceReference <| Maybe.withDefault "" operation.invoice.invoiceReference
                , callback "date facture" SetInvoiceDate <| Maybe.withDefault "" operation.invoice.invoiceDate
                , callback "montant facture" SetInvoiceAmount operation.invoice.invoiceAmount.stringValue
                , callback "fournisseur" SetStore operation.store
                , callback "commentaire" SetComment <| Maybe.withDefault "" operation.comment
            ]

viewOperationReadOnly: String -> (String -> Msg) -> String -> Html Msg
viewOperationReadOnly label msg val =
    tr [] [th [] [text label]
          , td [] [text val]
          ]

viewOperationInput: String -> (String -> Msg) -> String -> Html Msg
viewOperationInput label msg val =
    tr [] [th [] [text label]
          , td [] [input [ type_ "text", value val, onInput msg] []]
          ]

viewOperationFooter: Modal -> List (Html Msg)
viewOperationFooter modal =
    case modal of
        ModifyOperationModal 
            -> [button [class "button is-success", onClick SaveModifiedOperationClicked] [ text "Enregistrer"]
               , button [class "button is-warning", onClick CloseOperationModalClicked] [ text "Supprimer"]
               , button [class "button", onClick CloseOperationModalClicked] [ text "Annuler"]
               ]
        _ -> [emptyDiv] 



